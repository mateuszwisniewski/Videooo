apply plugin: 'jacoco'

def test = rootProject.ext.testing

jacoco {
    toolVersion = "$test.jacoco"
}

def moduleExcludes = [
        'app' : ['**/R.class',
                 '**/R$*.class',
                 '**/*$ViewInjector*.*',
                 '**/*$ViewBinder*.*',
                 '**/BuildConfig.*',
                 '**/Manifest*.*',
                 '**/VideoooApplication*.*',
                 '**/*Activity.class',
                 '**/*Module*.*',
                 '**/*Component*.*',
                 '**/*Extension.*',
                 '**/data/**'],
        'core':
                ['**/data/**']
]

def moduleClassDirs = [
        'app' : "$buildDir/intermediates/classes/debug",
        'core': "$buildDir/classes"
]

def moduleKotlinClassDirs = [
        'app' : "$buildDir/tmp/kotlin-classes/debug",
        'core': "$buildDir/classes"
]

def moduleJacocoExec = [
        'app' : "$buildDir/jacoco/testDebugUnitTest.exec",
        'core': "$buildDir/jacoco/test.exec"
]

task testReport(type: JacocoReport, dependsOn: "test") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports..."

    def debugFileTree = fileTree(dir: moduleClassDirs[project.name], excludes: moduleExcludes[project.name])
    def debugKotlinFileTree = fileTree(dir: moduleKotlinClassDirs[project.name], excludes: moduleExcludes[project.name])

    classDirectories = files([debugFileTree, debugKotlinFileTree])

    def coverageSourceDirs = [
            "src/main/java"
    ]
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files(moduleJacocoExec[project.name])

    reports {
        html.enabled true
        csv.enabled false
        xml {
            enabled true
            destination file("${buildDir}/reports/jacoco/report.xml")
        }
    }
}