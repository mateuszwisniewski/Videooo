apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../quality/jacoco.gradle'
apply from: '../quality/ktlint.gradle'

def config = rootProject.ext.configuration

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId 'com.wisnia.videooo'
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 1
        versionName '0.0.1'
        buildConfigField 'String', 'tmdb_api_key', config.tmdb_api_key
    }

    signingConfigs {
        release {
            try {
                storeFile file(signing.keystore)
                storePassword signing.keystore_password
                keyAlias signing.alias
                keyPassword signing.alias_password
            } catch (ignored) {
                throw new InvalidUserDataException("You have to define: " +
                        "keystore, keystore_password, alias, alias_password " +
                        "in secure/keystore.properties file")
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {

    implementation project(':core')

    // Kotlin
    implementation library.kotlin
    implementation library.coreKtx

    // Android Architecture
    implementation library.lifecycleExtensions

    // Android Support
    implementation library.appcompat
    implementation library.constraintLayout
    implementation library.material

    // Dagger
    implementation library.dagger
    implementation library.daggerSupport
    kapt library.daggerProcessor
    kapt library.daggerCompiler

    // Retrofit
    implementation library.retrofit
    implementation library.retrofitAdapter
    implementation library.retrofitConverter

    // Tests
    testImplementation tests.junit
}
